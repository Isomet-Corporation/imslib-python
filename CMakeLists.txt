cmake_minimum_required(VERSION 3.15)

message(STATUS "Building for Platform: '${CMAKE_GENERATOR_PLATFORM}'")
cmake_policy(SET CMP0091 NEW) # Required to avoid Conan toolchain error on Windows

project(imslib-python)

# Use C++17 or later
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

# --- Conan integration ------------------------------------------------------
set(CONAN_GENERATORS_DIR "${CMAKE_BINARY_DIR}/conan")
file(MAKE_DIRECTORY ${CONAN_GENERATORS_DIR})

if (MSVC)
   set(CONAN_TOOLCHAIN_FILE "${CONAN_GENERATORS_DIR}/build/generators/conan_toolchain.cmake")
else()
   set(CONAN_TOOLCHAIN_FILE "${CONAN_GENERATORS_DIR}/build/Release/generators/conan_toolchain.cmake")
endif()

if(NOT EXISTS "${CONAN_TOOLCHAIN_FILE}")
    message(STATUS "[CMake] Running Conan install")
    execute_process(
        COMMAND conan install ${CMAKE_SOURCE_DIR}
                --profile default
                -s build_type=Release
                -s compiler.cppstd=17
                --build=missing
                -of ${CONAN_GENERATORS_DIR}
        RESULT_VARIABLE result
        OUTPUT_VARIABLE out
        ERROR_VARIABLE err
    )
    message(STATUS "Conan output:\n${out}\n${err}")
    if(NOT result EQUAL 0)
        message(FATAL_ERROR "Conan install failed")
    endif()
endif()

message(STATUS "Conan toolchain at ${CONAN_TOOLCHAIN_FILE}")
include(${CONAN_TOOLCHAIN_FILE})

# --- SWIG / Python ----------------------------------------------------------
find_package(Python3 3.8 REQUIRED COMPONENTS Interpreter Development.Module)
if(Python3_FOUND)
  message(STATUS "Found Python3 at ${Python3_EXECUTABLE}")
  message(STATUS "Python include dirs: ${Python3_INCLUDE_DIRS}")
  message(STATUS "Python3 Library: ${Python3_LIBRARY}")
else()
  message(FATAL_ERROR "No Python3 installation found")
endif()

find_package(SWIG 4.0 COMPONENTS python REQUIRED)
if(SWIG_FOUND)
  message(STATUS "SWIG found: ${SWIG_EXECUTABLE}")
  if(NOT SWIG_python_FOUND)
    message(WARNING "SWIG Python bindings cannot be generated")
  endif()
endif()

include(${SWIG_USE_FILE})

set(SWIG_MODULE_NAME imslib)

# The underlying C++ Library
set(IMS_LIB_SUBMODULE ims-lib)

## iMS Dependencies generated by conan
#include(${CMAKE_BINARY_DIR}/generators/conan_toolchain.cmake) # Conan Toolchain (CMakeToolchain)
##
## Check for LibXML2
##
find_package(LibXml2 2.9.9 REQUIRED)
if (LibXml2_FOUND)
    message("-- Including LibXml2")
    include_directories(${libxml2_INCLUDE_DIRS})
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${libxml2_DEFINITIONS}")
else()
    message("-- LibXml2 not Found")
endif()

##
## Get BOOST
##
find_package(Boost REQUIRED)

# SWIG options:
#  -c++        : C++ mode
#  -threads    : Python GIL-safe callback support
#  -directors  : Support SWIG Callbacks
#  -Wall       : Warnings
set(CMAKE_SWIG_FLAGS "-c++" "-threads"  "-directors" "-Wall")

# Define the SWIG module
set(SWIG_INTERFACE swig/imslib.i)

set(CMAKE_SWIG_OUTDIR ${CMAKE_CURRENT_SOURCE_DIR}/python)

set_property(SOURCE ${SWIG_INTERFACE} PROPERTY CPLUSPLUS ON)

set (UseSWIG_TARGET_NAME_PREFERENCE STANDARD)
swig_add_library(${SWIG_MODULE_NAME}
    TYPE MODULE
    LANGUAGE python
    SOURCES ${SWIG_INTERFACE}
) 

# Set source and include directories
target_include_directories(${SWIG_MODULE_NAME} PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${Python3_INCLUDE_DIRS}
    ${CMAKE_CURRENT_SOURCE_DIR}/${IMS_LIB_SUBMODULE}/include
)

# Add iMS Library directly to this module
set(IMS_TARGET_NAME ${SWIG_MODULE_NAME})
add_subdirectory(${IMS_LIB_SUBMODULE})

target_link_directories(${SWIG_MODULE_NAME} PRIVATE ${Python3_LIBRARY_DIRS})
target_link_libraries(${SWIG_MODULE_NAME} PRIVATE Python3::Module ${IMS_LIBRARY_NAME})

if(MSVC)
    add_compile_options(/wd8029)  # Disable warning MSB8029 about building in Temporary Folder
endif()

# Configure output directory to match a package layout inside the build tree
set(DEST_PY_PACKAGE_DIR ${CMAKE_BINARY_DIR}/${SWIG_MODULE_NAME})
file(MAKE_DIRECTORY ${DEST_PY_PACKAGE_DIR})

message(STATUS "SWIG Python module will be in: ${CMAKE_SWIG_OUTDIR}")

# Install the compiled module into the package dir as well
install(TARGETS ${SWIG_MODULE_NAME}
  LIBRARY DESTINATION ${SWIG_MODULE_NAME}
  RUNTIME DESTINATION ${SWIG_MODULE_NAME}
  ARCHIVE DESTINATION ${SWIG_MODULE_NAME}
  PUBLIC_HEADER DESTINATION include
)

# Install the Python wrapper generated by SWIG
install(FILES ${CMAKE_SWIG_OUTDIR}/${SWIG_MODULE_NAME}.py
        DESTINATION ${SWIG_MODULE_NAME})

# Optional: create a small __init__.py so 'imslib' is a proper package
file(WRITE ${DEST_PY_PACKAGE_DIR}/__init__.py "from .imslib import *\n")
install(FILES "${DEST_PY_PACKAGE_DIR}/__init__.py" DESTINATION ${SWIG_MODULE_NAME})
